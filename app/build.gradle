apply plugin: 'com.android.application'

project.ext.KEYSTORE_PASSWORD = project.hasProperty("pass") ? "$pass" : '';

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "zlotindaniel.memorize"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 3
        versionName "1.0.2"

        testInstrumentationRunner "zlotindaniel.memorize.MemorizeE2ETestRunner"
    }
    signingConfigs {
        release {
            storeFile file('../../zlotindaniel.keystore')
            keyAlias 'daniel zlotin'
            storePassword KEYSTORE_PASSWORD
            keyPassword KEYSTORE_PASSWORD
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release;
        }
    }

    testOptions {
        unitTests.all { t ->
            reports {
                html.enabled true
            }
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
            }
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "      ${result.resultType} (${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)     "
                    def repeatLength = output.length()
                    println '\n\n' + ('-' * repeatLength) + '\n' + output + '\n' + ('-' * repeatLength) + '\n'

                    println "see report at file://${t.reports.html.destination}/index.html"
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.google.firebase:firebase-core:10.2.0'
    compile 'com.google.firebase:firebase-database:10.2.0'

    compile 'com.google.guava:guava:23.1-android'

    // unit tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:2.5.0'
    testCompile 'org.mockito:mockito-core:2.7.5'
    testCompile 'org.json:json:20140107'

    // e2e
    androidTestCompile 'org.assertj:assertj-core:2.5.0'
    androidTestCompile('com.android.support.test:runner:0.5') {
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.5') {
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.uiautomator:uiautomator-v18:2.1.2') {
        exclude module: 'support-annotations'
    }
}

task unit(dependsOn: 'testDebugUnitTest') {
    mustRunAfter 'clean'
}
task e2e(dependsOn: 'connectedDebugAndroidTest') {
    mustRunAfter 'unit'
}
task all(dependsOn: ['clean', 'unit', 'e2e']) {}

apply plugin: 'com.google.gms.google-services'
