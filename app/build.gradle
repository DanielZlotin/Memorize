apply plugin: 'com.android.application'
apply from: '../prepare-robolectric.gradle'


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "zlotindaniel.memorize"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "zlotindaniel.memorize.MemorizeE2ETestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all { t ->
            reports {
                html.enabled true
            }
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
            }
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "      ${result.resultType} (${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)     "
                    def repeatLength = output.length()
                    println '\n\n' + ('-' * repeatLength) + '\n' + output + '\n' + ('-' * repeatLength) + '\n'

                    println "see report at file://${t.reports.html.destination}/index.html"
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.google.firebase:firebase-core:10.2.0'
    compile 'com.google.firebase:firebase-database:10.2.0'

    // unit tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.3'
    testCompile 'org.assertj:assertj-core:2.5.0'
    testCompile 'org.mockito:mockito-core:2.7.5'

    // e2e
    androidTestCompile 'org.assertj:assertj-core:2.5.0'
//    androidTestCompile 'com.android.support:support-annotations:23.0.0'
    androidTestCompile ('com.android.support.test:runner:0.5') {
        exclude module: 'support-annotations'
    }
    androidTestCompile ('com.android.support.test:rules:0.5') {
        exclude module: 'support-annotations'
    }
    androidTestCompile ('com.android.support.test.uiautomator:uiautomator-v18:2.1.2') {
        exclude module: 'support-annotations'
    }
}

task unit(dependsOn: 'testDebugUnitTest') {}
task e2e(dependsOn: 'connectedDebugAndroidTest') {
    mustRunAfter 'unit'
}
task testAll(dependsOn: ['unit', 'e2e']) {}


apply plugin: 'com.google.gms.google-services'
